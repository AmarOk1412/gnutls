helpnode "Certtool help\nUsage : certtool [options]"

#int action;
option (s, generate-self-signed) { $action=0; } "Generate a self-signed certificate."

option (c, generate-certificate) { $action=4; } "Generate a signed certificate."

option (p, generate-privkey) { $action=1; } "Generate a private key."

option (q, generate-request) { $action=3; } "Generate a PKCS #10 certificate request."

option (e, verify-chain) { $action=5; } "Verify a certificate chain. The last certificate in the chain must be a self signed one."

#char *privkey;
option (load-privkey) STR "FILE" { $privkey = $1 } "Private key file to use."

#char *ca_privkey;
option (load-ca-privkey) STR "FILE" { $ca_privkey = $1 } "Certificate authority's private key file to use."

#char *ca;
option (load-ca-cert) STR "FILE" { $ca = $1 } "Certificate authority's certificate file to use."

option (i, cert-info) { $action = 2; } "Print information on a certificate."

option (k, key-info) { $action = 6; } "Print information on a private key."


#int pkcs8;
option (8, pkcs8) { $pkcs8=1 } "Use PKCS #8 format for private keys."

#int bits;
option (bits) INT "BITS" { $bits = $1 } "specify the number of bits for key generation."

#int debug;
option (d, debug) INT "LEVEL" { $debug = $1 } "specify the debug level. Default is 1."

option (h, help) { gaa_help(); exit(0); } "shows this help text"

option (v, version) { certtool_version(); exit(0); } "shows the program version"

init { $bits = 1024; $pkcs8 = 0; $privkey = NULL; $ca=NULL; $ca_privkey = NULL; 
	$debug=1; }

